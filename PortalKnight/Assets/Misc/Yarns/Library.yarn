title: HUB_introduction
position: -277,109
---
<<if not visited("HUB_introduction")>>
	Voice 1: Oh brave traveler, welcome. We've not seen a soul in centuries.
	Voice 2: Come for the promised riches atop the castle, have you?
	Voice 3: Unfortunately, you came unprepared. Without the Light's protection, you are powerless against the monstrousities that dwells within.
	-> ...
	Voice 3: If you insists on going forward, take Lightbringer. 
	<<equip_sword>>
	Voice 3: May it aid you in banishing the evils that lurk the castle's halls.
	<<jump HUB_inquire>>
<<endif>>
===
title: HUB_enter
position: -277,217
---
<<if not visited("HUB_enter")>>
	Distant Voices: Oh brave traveler, what brought you here to these ruins?
	<<camera_pan mural>>
	<<wait 1>>
	Distant Voices: Come. 
	<<wait 1>>
	<<camera_pan follow>>
<<endif>>
===
title: HUB_inquire
position: 17,126
---
<<if visited("HUB_inquire") and not visited("Lore_ThreeKnights")>>
	Voices: What else do you want to know?
<<elseif visited("Lore_ThreeKnights")>>
	The Knights: What else do you want to know?
<<else>>
	Voices: Before you go, is there anything you wish to know?
<<endif>>
-> The castle's history <<if not visited("Lore_CastleHistory")>>
	<<jump Lore_CastleHistory>>
-> Voices' identity <<if not visited("Lore_ThreeKnights") and visited("Lore_CastleHistory")>>
	Voice 1: Never thought you would ask.
	<<jump Lore_ThreeKnights>>
-> The three knights <<if not visited("Lore_ThreeKnights") and visited("Lore_CastleHistory")>>
	<<jump Lore_ThreeKnights>>
-> The knights' fate <<if not visited("Lore_Invasion") and visited("Lore_ThreeKnights")>>
	<<jump Lore_Invasion>>
-> Embark
	<<if not visited("Lore_ThreeKnights")>>
		Voice 1: These passages leads to the throne room, where the source of darkness resides.
		Voice 1: Go forth, brave traveler. Reclaim our castle.
		Voice 2: May the Light's grace protect your heart.
	<<else>>
		<<if visited("Lore_Invasion")>>
			Vega the Mighty: These passages leads directly to the throne room. Seek the source of the curse.
			Altair the Swift: Go forth, brave traveler. Avenge us!
		<<else>>
			Vega the Mighty: These passages leads to the throne room, where the source of darkness resides.
			Altair the Swift: Go forth, brave traveler. Reclaim our castle.
		<<endif>>
		Deneb the Wise: May the Light's grace warm your heart.
	<<endif>>
===
title: Lore_ThreeKnights
position: 29,298
---
Voice 1: Vega the Mighty.
Voice 2: Altair the Swift.
Voice 3: Deneb the Wise.
Vega the Mighty: Those are our titles before death. Now we are but wandering souls, trapped waiting for travelers like yourself to rid the castle of its monsters.
<<jump HUB_inquire>>
===
title: Lore_CastleHistory
position: -43,-67
---
Voice 1: Quite the inquisitive one, hmmm? If you wish to lend your ears, then pardon our reminiscing of the past.
Voice 1: Long ago, this land was home to the mighty kingdom of Constellia, and above us stood a dazzling castle. Constellia was known for its knight's prowest, and three in particular were crowned lords.
<<jump HUB_inquire>>
===
title: Lore_Invasion
position: 257,33
---
Deneb the Wise: We met our ends by a terrible curse that darkened the sun. Monsters emerged from the shadows left behind by the disappeared sun, whose etheral form no physical weapon can scratch.
Vega the Mighty: Oh brave traveller, we put up great resistance against the creatures of the curse. But our hands eventually grew feeble with thirst and hunger.
Vega the Mighty: Deneb here had the wise idea of combining our powers to create Lightbringer, a weapon imbued with the Light's blessings.
Altair the Swift: We reached the throne room where the source of the curse resides, but were too weak to finish the battle.
<<jump HUB_inquire>>
===
